MODULE Main;

REQUIRE System;

CLASS Team 'Команды';
number 'Номер' = DATA INTEGER  (Team) IN id;
WHEN SET(Team t IS Team) AND NOT number(t) DO {
    number(t) <- (GROUP MAX number(Team tt)) (+) 1;
}
name 'Название команды' = DATA STRING[30] (Team) IN base NONULL;

CLASS Players 'Игроки';
number 'Номер' = DATA INTEGER  (Players) IN id;
WHEN SET(Players p IS Players) AND NOT number(p) DO {
    number(p) <- (GROUP MAX number(Players pp)) (+) 1;
}
playerName 'Игрок' = DATA STRING[100] (Players) IN base NONULL;
team = DATA Team (Players) NONULL DELETE;
teamName 'Команда' (Players p) = name(team(p)) NONULL;

CLASS Game 'Игры';
number 'Номер' = DATA INTEGER (Game) IN id;
WHEN SET(Game g IS Game) AND NOT number(g) DO {
    number(g) <- (GROUP MAX number(Game gg)) (+) 1;
}
date 'Дата' = DATA DATE (Game) IN base NONULL;
hostTeam = DATA Team (Game) NONULL DELETE;
guestTeam = DATA Team (Game) NONULL DELETE;
hostTeamName 'Хозяева' (Game game) = name(hostTeam(game)) IN base NONULL;
guestTeamName 'Гости' (Game game) = name(guestTeam(game)) IN base NONULL;
CONSTRAINT hostTeam(Game team) = guestTeam(team) CHECKED BY hostTeam, guestTeam 
    MESSAGE 'Хозяйская и гостевая команды должны быть разными';
hostGoals 'Х голы' = DATA INTEGER (Game);
guestGoals 'Г голы' = DATA INTEGER (Game);
CONSTRAINT hostGoals(Game game) = guestGoals(game) 
    MESSAGE 'Игра не может закончиться вничью';
winner(Game game) = IF hostGoals(game) > guestGoals(game)
    THEN hostTeam(game)
    ELSE guestTeam(game);
looser(Game game) = IF hostGoals(game) > guestGoals(game)
    THEN guestTeam(game)
    ELSE hostTeam(game);

CLASS Goals 'Голы';
number 'Номер' = DATA INTEGER (Goals) IN id;
WHEN SET(Goals g IS Goals) AND NOT number(g) DO {
    number(g) <- (GROUP MAX number(Goals gg)) (+) 1;
}
game = DATA Game (Goals) NONULL DELETE;
numberGame 'Номер игры' (Goals g) = number(game(g)) NONULL;
player = DATA Players (Goals) NONULL DELETE;
playerName 'Игрок' (Goals g) = playerName(player(g)) NONULL;
goals 'Голы' = DATA INTEGER (Goals) NONULL;
CONSTRAINT (team(player(Goals g)) != hostTeam(game(g))) 
    AND (team(player(g)) != guestTeam(game(g))) 
    MESSAGE 'Команда указанного игрока не играла в данном матче!';
hostGoalsByGame = GROUP
    SUM goals(Goals g)
    IF team(player(g)) == hostTeam(game(g))
    BY game(g);
    CONSTRAINT hostGoalsByGame(Game g) > hostGoals(g) MESSAGE 'Количество голов больше чем в результатах матча!';
guestGoalsByGame = GROUP
    SUM goals(Goals g)
    IF team(player(g)) == guestTeam(game(g))
    BY game(g);
    CONSTRAINT guestGoalsByGame(Game g) > guestGoals(g) MESSAGE 'Количество голов больше чем в результатах матча!';


countGoals 'Количество голов' = GROUP SUM goals(Goals g) BY player(g);

CLASS GameResult 'Р/И' {
    win 'П',
    winOT 'ПО',
    winSO 'ПБ'
}

resultName 'Имя' (GameResult game) = staticCaption(game) IF game IS GameResult IN base;
userResult = DATA GameResult (Game);
result (Game game) = OVERRIDE userResult(game), 
    (GameResult.win IF ((hostGoals(game) (-) guestGoals(game)) > 1 OR (guestGoals(game) (-) hostGoals(game)) > 1));
resultName 'Р/И' (Game game) = resultName(result(game));
CONSTRAINT ((hostGoals(Game game) (-) guestGoals(game)) > 1 
    OR (hostGoals(game) (-) guestGoals(game)) < -1) AND userResult(game) 
    MESSAGE 'Результат игры определен автоматически';
CONSTRAINT ((hostGoals(Game game) (-) guestGoals(game)) < 2 
    AND (hostGoals(game) (-) guestGoals(game)) > -2) AND NOT userResult(game) 
    MESSAGE 'Укажите результат игры';

hostGamesPlayed = GROUP SUM 1 BY hostTeam(Game game);
guestGamesPlayed = GROUP SUM 1 BY guestTeam(Game game);
gamesPlayed 'И' (Team team) = hostGamesPlayed(team) (+) guestGamesPlayed(team);

gamesWonBy(Team team, GameResult type) = 
    OVERRIDE [GROUP SUM 1 BY winner(Game game), result(game)](team, type), 0;
gamesWon 'В' (Team team) = gamesWonBy(team, GameResult.win);
gamesWonOT 'ВО' (Team team) = gamesWonBy(team, GameResult.winOT);
gamesWonSO 'ВБ' (Team team) = gamesWonBy(team, GameResult.winSO);

gamesLostBy(Team team, GameResult type) = 
    OVERRIDE [GROUP SUM 1 BY looser(Game game), result(game)](team, type), 0;
gamesLost 'П' (Team team) = gamesLostBy(team, GameResult.win);
gamesLostOT 'ПО' (Team team) = gamesLostBy(team, GameResult.winOT);
gamesLostSO 'ПБ' (Team team) = gamesLostBy(team, GameResult.winSO);

points 'Очки' (Team team) = 
    gamesWon(team) * 3 + (gamesWonSO(team) + gamesWonOT(team)) * 2 + gamesLostOT(team) + gamesLostSO(team);

hostGoalsScored = 
    GROUP SUM hostGoals(Game game) BY hostTeam(game);
guestGoalsScored = 
    GROUP SUM guestGoals(Game game) BY guestTeam(game);
goalsScored 'Кол-во забитых голов' (Team team) = 
    OVERRIDE hostGoalsScored(team) (+) guestGoalsScored(team), 0 IF team IS Team;

hostGoalsConceded = 
    GROUP SUM guestGoals(Game game) BY hostTeam(game);
guestGoalsConceded = 
    GROUP SUM hostGoals(Game game) BY guestTeam(game);
goalsConceded 'Кол-во пропущенных голов' (Team team) = 
    OVERRIDE hostGoalsConceded(team) (+) guestGoalsConceded(team), 0 IF team IS Team;

place 'Место' (Team team) = PARTITION SUM 1 
    ORDER DESC points(team), gamesWon(team), gamesWonOT(team), gamesWonSO(team),
    (OVERRIDE goalsScored(team) (-) goalsConceded(team), 0), goalsScored(team);
    
FORM TeamsList 'Список команд'
    OBJECTS  team = Team
    PROPERTIES(team) number, name, NEW, DELETE 
    ORDER number(team), name(team)
;

FORM PlayersList 'Список игроков'
    OBJECTS players = Players
    PROPERTIES(players) playerName, teamName, NEW, DELETE
    ORDER teamName(players), playerName(players)
;
FORM GameGoals 'Игры и голы'
    OBJECTS game = Game
    PROPERTIES(game) date, hostTeamName, hostGoals, guestGoals, guestTeamName, resultName, NEW, DELETE
    
    OBJECTS goals = Goals
    PROPERTIES(goals) numberGame, playerName, goals, NEW, DELETE
    ORDER numberGame(goals)
;

FORM BestPlayers 'Бомбардиры'
    OBJECTS best 'Лучшие игроки' = Players
    PROPERTIES(best) countGoals, playerName
    ORDER countGoals(best) DESC, playerName(best) 
    FILTERS countGoals(best) > 0
;

FORM TournamentTable 'Турнирная таблица'
    OBJECTS team = Team
    PROPERTIES(team) place, name, gamesPlayed, gamesWon, gamesWonOT, gamesWonSO,
                     gamesLostSO, gamesLostOT, gamesLost, goalsScored, goalsConceded, points//, NEW, DELETE
    ORDER place(team)
;

NAVIGATOR {
    NEW FOLDER matches 'Данные о матчах' WINDOW toolbar FIRST {
        NEW TeamsList;
        NEW PlayersList;
        NEW GameGoals;
        NEW BestPlayers;
        NEW TournamentTable;
    }
}
